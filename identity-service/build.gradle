plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.2'
	id 'io.spring.dependency-management' version '1.1.2'
	id 'jacoco'
	id 'org.sonarqube' version '4.4.1.3373'
}

group = 'com.syed'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'com.microsoft.azure:applicationinsights-runtime-attach:3.4.16'

	compileOnly 'org.projectlombok:lombok'

	runtimeOnly 'org.postgresql:postgresql'

	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation "org.testcontainers:testcontainers:1.19.0"
	testImplementation "org.testcontainers:junit-jupiter:1.19.0"
	testImplementation "org.testcontainers:postgresql:1.19.0"
	testImplementation 'io.cucumber:cucumber-junit:6.10.4'
	testImplementation 'io.cucumber:cucumber-java:6.10.4'
	testImplementation 'io.cucumber:cucumber-spring:6.10.4'
	testImplementation 'org.junit.vintage:junit-vintage-engine:5.7.2'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}

	integrationTestImplementation.extendsFrom(testImplementation)
	integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
	functionalTestImplementation.extendsFrom(testImplementation)
	functionalTestRuntimeOnly.extendsFrom(testRuntimeOnly)
	cucumberRuntime.extendsFrom(testImplementation)
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.required = true
		csv.required = false
		xml.outputLocation = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
	}
}

sonarqube {
	properties {
		property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
	}
}

sourceSets {
	integrationTest {
		java.srcDir 'src/integrationTest/java'
		resources.srcDir 'src/integrationTest/resources'
		compileClasspath += sourceSets.main.output + sourceSets.test.output
		runtimeClasspath += sourceSets.main.output + sourceSets.test.output

		// remove below error
		// Entry create-app.sql is a duplicate but no duplicate handling strategy has been set
		resources.exclude '**/*.sql'
	}

	functionalTest {
		java.srcDir 'src/functionalTest/java'
		resources.srcDir 'src/functionalTest/resources'
		compileClasspath += sourceSets.main.output + sourceSets.test.output
		runtimeClasspath += sourceSets.main.output + sourceSets.test.output

		// to remove error Entry features/*.feature is a duplicate but no duplicate handling strategy has been set
		// duplicate file added to ./build/..
		resources.exclude '**/features/**'
	}
}

task integrationTest(type: Test) {
	useJUnitPlatform()

	description = "Run integration tests"
	group = "verification"
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath + files('src/integrationTest/resources')
	//  +files(..) - ensures `src/integrationTest/resources directory` is
	//  part of classpath during the execution of the integration tests
}

task functionalTest(type: Test) {
	useJUnitPlatform()

	description = "Run functional tests"
	group = "verification"
	testClassesDirs = sourceSets.functionalTest.output.classesDirs
	classpath = sourceSets.functionalTest.runtimeClasspath
}